buildscript {
	ext.kotlin_version = '1.3.71'

	repositories {
		google()
		jcenter()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:3.6.3'
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.71"
	}
}

allprojects {
	repositories {
		google()
		jcenter()
		mavenLocal()
	}
}
def javaLibs = [
	'server.guru',
	'shared.serial'
]

subprojects {
	if (javaLibs.contains(project.name)) {
		apply plugin: 'java-library'
		apply plugin: 'kotlin'
		dependencies {
			implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
		}
	} else {
		if (project.name == 'app') apply plugin: 'com.android.application'
		else apply plugin: 'com.android.library'
		apply plugin: 'kotlin-android'
		apply plugin: 'kotlin-android-extensions'
		android {
			compileSdkVersion 29
			buildToolsVersion "29.0.3"

			defaultConfig {
				minSdkVersion 21
				targetSdkVersion 29
				versionCode 2
				versionName "2.0"
			}
		}
		dependencies {
			implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
			implementation 'androidx.core:core-ktx:1.2.0'
		}
	}
}